# ----------------------------------------------------------------------- #
# ------------------- Check that called with component ------------------ #
# ----------------------------------------------------------------------- #
if (NOT qtils_FIND_COMPONENTS)
    message(FATAL_ERROR "find_package(qtils) called with no component")
endif()
# ----------------------------------------------------------------------- #
# -------------- Set config_name dependent on libraries type ------------ #
# ----------------------------------------------------------------------- #
if(QTILS_STATIC_LIBRARIES)
    set(config_name "static")
else()
    set(config_name "shared")
endif()
# ----------------------------------------------------------------------- #
# ---------------------- Update requested components -------------------- #
# ----------------------------------------------------------------------- #
set(FIND_qtils_ADDITIONAL_COMPONENTS "")

foreach(component ${qtils_FIND_COMPONENTS})
    list(APPEND FIND_qtils_ADDITIONAL_COMPONENTS ${FIND_qtils_${component}_DEPENDENCIES})
endforeach()

list(APPEND qtils_FIND_COMPONENTS ${FIND_qtils_ADDITIONAL_COMPONENTS})
list(REMOVE_DUPLICATES qtils_FIND_COMPONENTS)
# ----------------------------------------------------------------------- #
# -------------------- Load targets and dependencies -------------------- #
# ----------------------------------------------------------------------- #
set(targets_config_file ${CMAKE_CURRENT_LIST_DIR}/qtils-${config_name}-targets.cmake)
set(qtils_FOUND TRUE)

if(EXISTS ${targets_config_file})
    include(${targets_config_file})
    include("${CMAKE_CURRENT_LIST_DIR}/qtils-config-dependencies.cmake")

    if (qtils_FOUND)
        foreach (component ${qtils_FIND_COMPONENTS})
            if (TARGET qtils-${component})
                set(qtils_${component}_FOUND TRUE)
            else()
                set(FIND_qtils_ERROR "Found qtils but requested component '${component}' is missing in the config defined in ${QTILS_DIR}.")
                set(qtils_${component}_FOUND FALSE)
                set(qtils_FOUND FALSE)
            endif()
        endforeach()
    endif()
else()
    set(FIND_qtils_ERROR "Requested qtils configuration (${config_name}) was not found")
    set(qtils_FOUND FALSE)
endif()
# ----------------------------------------------------------------------- #
# ------------=------------- Display all errors ------------------------- #
# ----------------------------------------------------------------------- #
if (NOT qtils_FOUND)
    if(qtils_FIND_REQUIRED)
        message(FATAL_ERROR "${FIND_qtils_ERROR}")
    elseif(NOT qtils_FIND_QUIETLY)
        message(STATUS "${FIND_qtils_ERROR}")
    endif()
endif()
# ----------------------------------------------------------------------- #
